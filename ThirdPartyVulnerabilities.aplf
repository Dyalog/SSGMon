 r←ThirdPartyVulnerabilities interval;subject;m;list;msg;tsfmt;summary;http;tpf;targets;update;i;target;url;rc;last;newhash;changed;body;retried
 ⍝ Send SSGMon e-mail regarding 3rd party vulnerabilities
 ⍝ r is empty on success, else error message
 ⍝ if interval=¯1, report last changed date for all monitored URLs
 ⍝ if interval=0, any modified issues

 r←''
 subject←'Error' ⍝ If anything goes wrong en route
 :If ∨/m←0=≢¨MAILSERVER MAILFROM MAILTO SMTPPASS THIRDPARTYFILE
     r←'Missing configuration: ',⍕m/' '(=⊂⊢)' MAILSERVER MAILFROM MAILTO SMTPPASS THIRDPARTYFILE'
     →0
 :EndIf

 msg←list←''
 tsfmt←TSFmtNice ⍝ Was 'Mmm DD hh:mm'∘(1200⌶)
 summary←interval=¯1

 ⍝ Initalize HttpCommand used in Watch
 http←⎕NEW #.HttpCommand
 http.URL←''
 http.Command←'get'
 http.Timeout←30000 ⍝ (default=5000)

 tpf←⎕JSON⊃⎕NGET THIRDPARTYFILE
 targets←tpf.Targets
 update←0

 :For i :In ⍳≢targets
     target←i⊃targets
     http.URL←url←target.URL
     retried←0
    RETRY:
     rc←http.Run

     :If rc.rc=0
     :AndIf rc.HttpStatus=200
         last←tsfmt target.LastChanged
         url←'<a href="',url,'">',url,'</a>'
         ⍝ This is an attempt to cope with pages which are ?cached?load balanced? so may not have the same
         ⍝ source twice running, even though the page has effectively not changed.
         ⍝ More may be needed to be done.
         body←⊃('<body(.*)/body>'⎕S'\1'⎕OPT('Mode' 'M')('DotAll' 1))⊢rc.Data
         newhash←(⎕D,⎕A)[1+,⍉16 16⊤Sha256 ⎕UCS'UTF-8'⎕UCS body]
         changed←target.Sha256≢'' ⍝ we KNOW it changed
         :If target.Sha256≢newhash
             msg,←⊂(changed/'Changed: '),url,' (last update ',last,')<br />'
             :If changed ⍝ If hash actually changed, update the changed date (else leave as "unknown")
                 target.LastChanged←1 ⎕DT'Z'
             :EndIf
             target.Sha256←newhash
             update←1
         :EndIf
         :If summary ⍝ produce list
             list,←⊂url,' (last recorded update ',last,')<br />'
         :EndIf
     :ElseIf rc.rc=100 ⍝ Timeout
         msg,←'<b>Timed out: ',url,'</b><br />'
         retried+←1
         :If retried<5
            ⎕DL 60
            →RETRY
         :EndIf
     :Else
         msg,←'<b>UNABLE to check ',url,': ',(⍕rc.(rc HttpStatus)),'</b><br />'
     :EndIf
 :EndFor

 :If update
     (⊂⎕JSON⍠'Compact' 0⊢tpf) ⎕NPUT ⎕OPT'NEOL' 2⊢THIRDPARTYFILE 1
     ⎕←'Updated 3rd party json file'
 :EndIf

 subject←'Third Party Vulnerability Update',summary/' - With Overview'

 :If summary
     msg,←(⊂'<br />Third Party Vulnerability URLs monitored<br /><br />'),list
     ⎕←'Checked 3rd party vulnerability URLS'
 :EndIf

SEND:
 :If 0≠≢msg
     ⎕←'About to send 3rd party vulnerablility check list'
     SendMail MAILSERVER MAILFROM SMTPPASS MAILTO('SSGMon: ',subject)msg
     ⎕←'Sent 3rd party vulnerablility check list:'subject
     ⎕←msg
 :EndIf
