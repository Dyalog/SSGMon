 {msg}←SecurityMonitor interval;f;i;new;cols;old;tn;now;then;msg;subject;ns;tbl;tsfmt;gone;when;nums;m;last;cn
 ⍝ Return text of e-mail to send to SSG.
 ⍝ start is the timestamp of the last run in (1 ⎕DT) format
 ⍝ if interval=¯1, report all open issues (weekly run)
 ⍝ if interval=0, issues modified since last run (regardless of status)
 ⍝ else issues modified in last "interval" days (regardless of status)

 subject←'Error' ⍝ If anything goes wrong en route
 :If ∨/m←0=≢¨MAILSERVER MAILFROM SMTPPASS MAILTO LOGFILE SMTPPASS MANTISAPITOKEN
     msg←'Missing configuration: ',⍕m/' '(=⊂⊢) ' MAILSERVER MAILFROM SMTPPASS MAILTO LOGFILE SMTPPASS MANTISAPITOKEN'
     →SEND
 :EndIf

 tsfmt←'Mmm DD hh:mm'∘(1200⌶)
 cols←'id' 'updated' 'status' 'reporter' 'handler' 'summary'

 f←(⎕JSON MantisAPI'filters').filters.(id name)
 f←{(⍵⍳⊂'All Security Issues')⊃⍺}/↓⍉↑f ⍝ The filter ID

 ns←⎕JSON MantisAPI 'issues?filter_id=',⍕f
 new←↑ns.issues.(id updated_at status.name reporter.name handler.name summary) ⍝ New Status
 new[;2]←ISOtoDN¨new[;2] ⍝ ISO timestamps to UTC IDN

 now←1 ⎕DT'Z' ⍝ Current UTC time

 :If ⎕NEXISTS LOGFILE
     tn←LOGFILE ⎕FTIE 0
 :Else
     tn←LOGFILE ⎕FCREATE 0
     ⎕←'Created log file "',LOGFILE,'"'
     'Security Monitor Log File v1.0'⎕FAPPEND tn
     (9⍴⊂'reserved') ⎕FAPPEND¨tn
     (now new)⎕FAPPEND tn
 :EndIf

 :If 2=⍴⍴old←⎕FREAD tn,cn←¯1+2⊃⎕FSIZE tn ⍝ if last comonent is a matrix
     then←2 1 ⎕DT 3⊃⎕FRDCI tn,cn         ⍝    ... then get timestamp from component information
 :ElseIf 2=⍴old
     (then old)←old                      ⍝ ... else component should contain timestamp and data
 :Else
     msg←'Invalid file format'
     →SEND
 :EndIf

 :If old[;1 2]≢new[;1 2]   ⍝ Something changed?
     (now new)⎕FAPPEND tn  ⍝ Write a new record
 :EndIf

:Select interval
:Case ¯1 ⍝ All open issues
    tbl←(~new[;3]∊⊂'closed')⌿new
    subject←'List of All Open Security Issues'

:Case 0  ⍝ All changes since last run
    gone←(~old[;1]∊new[;1])⌿old ⍝ disappeared from the list
    gone[;3]←⊂'**UNFLAGGED**'
    tbl←gone⍪(~(↓new[;1 2])∊↓old[;1 2])⌿new
    subject←'Security Issues modified since ',tsfmt then

:Else    ⍝ Changes in last "interval" days
    tbl←(new[;2]>when←now-interval)⌿new
    subject←'Security Issues modified since ',tsfmt when

:EndSelect

  nums←⍕¨tbl[;1] ⍝ Now create Mantis Links
  tbl[;1]←'<a href="https://mantis.dyalog.com/view.php?id='∘,¨nums,¨⊂'>',¨nums,¨⊂'</a>'
  tbl[;2]←tsfmt tbl[;2]
    msg←1↓,(⎕UCS 13),⍕⍕¨cols⍪tbl
    ∘∘∘

SEND:
:If 0≠≢tbl
    SendMail MAILSERVER MAILFROM SMTPPASS MAILTO subject msg
:EndIf
